apply plugin: 'maven'
apply plugin: 'signing'

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName:  project.ext.publish_credentialsUserName, password: project.ext.publish_credentialsPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName:  project.ext.publish_credentialsUserName, password: project.ext.publish_credentialsPassword)
            }

            pom.project {
                name project.ext.publish_name
                packaging 'jar'
                description project.ext.publish_description
                url project.ext.publish_url

                scm {
                    url project.ext.publish_scm
                    connection project.ext.publish_scm
                    developerConnection project.ext.publish_scm
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url project.ext.publish_license
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id project.ext.publish_developerId
                        name project.ext.publish_developerName
                        email project.ext.publish_developerEmail
                    }
                }
            }
        }
    }
}

